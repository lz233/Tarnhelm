plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
}

android {
    namespace 'cn.ac.lz233.tarnhelm'
    compileSdk 36

    defaultConfig {
        Date date = new Date()
        applicationId "cn.ac.lz233.tarnhelm"
        minSdk 27
        targetSdk 36
        versionCode = date.format("yyyyMMdd").toInteger()
        versionName "1.8.2"

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas")
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions = ["version"]
    productFlavors {
        github {
            buildConfigField 'String', 'FLAVOR', '"github"'
        }
        google {
            buildConfigField 'String', 'FLAVOR', '"google"'
        }
        fdroid {
            buildConfigField 'String', 'FLAVOR', '"fdroid"'
        }
        coolapk {
            buildConfigField 'String', 'FLAVOR', '"coolapk"'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
        buildConfig true
        aidl true
    }
    packagingOptions {
        jniLibs {
            excludes += []
        }
        resources {
            excludes += ['META-INF/**', 'kotlin/**', 'okhttp3/**', 'org/**', '**.properties', '**.bin', '**kotlin**']
        }
    }
}

configurations.configureEach {
    exclude group: 'dev.rikka.rikkax.appcompat', module: 'appcompat'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":shizuku-service")

    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.room:room-runtime:2.7.2'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.1'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.3.0'
    implementation 'androidx.window:window:1.4.0'
    ksp 'androidx.room:room-compiler:2.7.2'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.9.0'

    // Visual Studio App Center Retirement: https://learn.microsoft.com/en-us/appcenter/retirement
    // githubImplementation 'com.microsoft.appcenter:appcenter-analytics:5.0.5'
    // githubImplementation 'com.microsoft.appcenter:appcenter-crashes:5.0.5'
    // googleImplementation 'com.microsoft.appcenter:appcenter-analytics:5.0.5'
    // googleImplementation 'com.microsoft.appcenter:appcenter-crashes:5.0.5'
    // coolapkImplementation 'com.microsoft.appcenter:appcenter-analytics:5.0.5'
    // coolapkImplementation 'com.microsoft.appcenter:appcenter-crashes:5.0.5'

    implementation 'com.google.android.material:material:1.12.0'
    implementation "dev.rikka.rikkax.material:material-preference:2.0.0"

    implementation 'com.guolindev.permissionx:permissionx:1.8.1'

    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.14'
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'

    compileOnly 'de.robv.android.xposed:api:82'

    def shizukuVersion = "13.1.5"
    implementation "dev.rikka.shizuku:api:$shizukuVersion"
    implementation "dev.rikka.shizuku:provider:$shizukuVersion"

    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:5.1'

    implementation 'io.github.billywei01:fastkv:2.6.0'
    implementation 'io.github.billywei01:packable-kotlin:2.1.3'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}
